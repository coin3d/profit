dnl ************************************************************************
dnl * configure.in for SIM's profit library.
dnl ************************************************************************

AC_INIT(include/profit/profit.h)

AC_PREREQ(2.13)

AC_PROG_CC
AM_PROG_CC_STDC

if test "x$am_cv_prog_cc_stdc" = "xno"; then
  AC_MSG_WARN(you probably need an ANSI compatible C compiler to build simage!)
fi

PROFIT_MAJOR_VERSION=0
PROFIT_MINOR_VERSION=9
PROFIT_MICRO_VERSION=0
PROFIT_INTERFACE_AGE=0
PROFIT_BINARY_AGE=0
PROFIT_VERSION=$PROFIT_MAJOR_VERSION.$PROFIT_MINOR_VERSION.$PROFIT_MICRO_VERSION

AC_SUBST(PROFIT_MAJOR_VERSION)
AC_SUBST(PROFIT_MINOR_VERSION)
AC_SUBST(PROFIT_MICRO_VERSION)
AC_SUBST(PROFIT_VERSION)
AC_SUBST(PROFIT_INTERFACE_AGE)
AC_SUBST(PROFIT_BINARY_AGE)

LT_RELEASE=$PROFIT_MAJOR_VERSION.$PROFIT_MINOR_VERSION
LT_CURRENT=`expr $PROFIT_MICRO_VERSION - $PROFIT_INTERFACE_AGE`
LT_REVISION=$PROFIT_INTERFACE_AGE
LT_AGE=`expr $PROFIT_BINARY_AGE - $PROFIT_INTERFACE_AGE`

AC_SUBST(LT_RELEASE)
AC_SUBST(LT_CURRENT)
AC_SUBST(LT_REVISION)
AC_SUBST(LT_AGE)

VERSION=$PROFIT_VERSION

AM_INIT_AUTOMAKE(libprofit, $VERSION)
AM_CONFIG_HEADER(config.h)

dnl AM_DISABLE_STATIC
AM_MAINTAINER_MODE
AM_PROG_LIBTOOL

dnl ************************************************************************
dnl * Check if host CPU is bigendian or littleendian
dnl ************************************************************************

AC_ARG_WITH(bigendian,
	[  --with-bigendian        set endianness for processor architecture
                          [default=auto]],
	[case "${withval}" in
		yes)  want_bigendian=yes ;;
		no)   want_bigendian=no ;;
		auto) want_bigendian=auto ;;
		*) AC_MSG_ERROR(bad value \"${withval}\" for --with-bigendian) ;;
	esac],
	[want_bigendian=auto])

if test "$want_bigendian" = "yes"; then
	AC_DEFINE(WORDS_BIGENDIAN, 1)
elif test "$want_bigendian" = "auto"; then
	AC_C_BIGENDIAN
	if test "$ac_cv_c_bigendian" = "unknown"; then
		AC_MSG_ERROR(couldn't decide on the architecture endianness - specify endianness with the --with-bigendian option)
	fi
fi

dnl ************************************************************************

AC_OUTPUT(Makefile \
	src/Makefile src/util/Makefile src/nodes/Makefile \
	examples/Makefile examples/fltdump/Makefile examples/fltstrip/Makefile \
        examples/fltnode/Makefile)

